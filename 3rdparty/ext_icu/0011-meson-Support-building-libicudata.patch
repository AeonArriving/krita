From 80a5487575ed15ac6c6a7ee940cc5dce74580e56 Mon Sep 17 00:00:00 2001
From: "L. E. Segovia" <amy@amyspark.me>
Date: Wed, 16 Nov 2022 19:32:23 -0300
Subject: [PATCH 11/18] meson: Support building libicudata

---
 source/data/meson.build     | 60 +++++++++++++++++++++++++++++++++++++
 source/meson.build          |  8 +++++
 source/stubdata/meson.build |  3 +-
 3 files changed, 69 insertions(+), 2 deletions(-)
 create mode 100644 source/data/meson.build

diff --git a/source/data/meson.build b/source/data/meson.build
new file mode 100644
index 0000000..ce6f3cb
--- /dev/null
+++ b/source/data/meson.build
@@ -0,0 +1,60 @@
+U_ICUDATA_NAME = 'icudt@0@'.format(U_ICU_VERSION.split('.')[0])
+
+icudata_command = [
+  genccode_exe, '-d', '@OUTDIR@', '-e', U_ICUDATA_NAME, '-f', U_ICUDATA_NAME, '@CURRENT_SOURCE_DIR@/in/@0@l.dat'.format(U_ICUDATA_NAME)
+]
+
+if host_machine.system() == 'windows' and get_option('default_library') == 'static'
+  icudata_command += ['--skip-dll-export']
+endif
+
+if host_machine.system() == 'windows'
+  if cpp.get_argument_syntax() == 'msvc'
+    icudata_asm_output = '@0@.c'.format(U_ICUDATA_NAME)
+  else
+    icudata_asm_output = '@0@.S'.format(U_ICUDATA_NAME)
+    icudata_command += ['-a', 'gcc-mingw64']
+  endif
+elif host_machine.system() == 'cygwin'
+  icudata_asm_output = '@0@.S'.format(U_ICUDATA_NAME)
+  icudata_command += ['-a', 'gcc-cygwin']
+elif host_machine.system() == 'darwin'
+  icudata_asm_output = '@0@.S'.format(U_ICUDATA_NAME)
+  icudata_command += ['-a', 'gcc-darwin']
+elif cpp.get_id() == 'gcc' or cpp.get_id() == 'clang'
+  icudata_asm_output = '@0@.S'.format(U_ICUDATA_NAME)
+  icudata_command += ['-a', 'gcc']
+else
+  icudata_asm_output = '@0@.c'.format(U_ICUDATA_NAME)
+endif
+
+icudata_asm = custom_target(
+    'icudata_asm',
+    command: icudata_command,
+    output: icudata_asm_output
+)
+
+sources = [icudata_asm]
+
+if host_machine.system() == 'windows'
+  sources += windows.compile_resources(
+    'misc/icudata.rc',
+    include_directories: incdir
+  )
+endif
+
+icudata = library(
+    icudata_name,
+    sources,
+    include_directories: incdir,
+    version: U_ICU_VERSION,
+    install: true,
+)
+
+icudata_dep = declare_dependency(
+  compile_args: usage_args,
+)
+
+if meson.version().version_compare('>=0.54.0')
+  meson.override_dependency('icu-data', icudata_dep)
+endif
diff --git a/source/meson.build b/source/meson.build
index 91e06a0..dcb9290 100644
--- a/source/meson.build
+++ b/source/meson.build
@@ -1,6 +1,12 @@
 incdir = include_directories('common', 'i18n')
 toolinc = include_directories('tools/toolutil')
 
+if host_machine.system() == 'windows'
+  icudata_name = '@0@icudt@1@'.format(library_prefix, library_suffix)
+else
+  icudata_name = '@0@icudata@1@'.format(library_prefix, library_suffix)
+endif
+
 subdir('stubdata')
 subdir('common')
 subdir('i18n')
@@ -10,4 +16,6 @@ subdir('io')
 
 subdir('tools')
 
+subdir('data')
+
 subdir('test')
diff --git a/source/stubdata/meson.build b/source/stubdata/meson.build
index 506297d..76912bd 100644
--- a/source/stubdata/meson.build
+++ b/source/stubdata/meson.build
@@ -10,11 +10,10 @@ if host_machine.system() == 'windows'
 endif
 
 stubdata_lib = library(
-  'stubdata',
+  icudata_name,
   sources,
   c_args: '-DSTUBDATA_BUILD',
   cpp_args: '-DSTUBDATA_BUILD',
   include_directories: incdir,
   version: U_ICU_VERSION,
-  install: true,
 )
-- 
2.37.1.windows.1

